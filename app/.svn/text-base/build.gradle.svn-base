apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.greenrobot.greendao'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.0'
    defaultConfig {
        applicationId "com.frxs.WMS"
        minSdkVersion 14
        versionCode 2
        versionName "1.9.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    //配置keystore签名
    signingConfigs {
        release {
            try {
                storeFile file("frxs_key")
                storePassword KEYSTORE_PASSWORD
                keyAlias "frxsapp"
                keyPassword KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "isDebug", "false"
            signingConfig signingConfigs.release
        }

        debug {
            versionNameSuffix "-debug"
            buildConfigField "boolean", "isDebug", "true"
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->    //批量修改Apk名字
        variant.outputs.all { output ->
            if (!variant.buildType.isDebuggable()) {
                //获取签名的名字 variant.signingConfig.name
                //要被替换的源字符串
                def sourceFile = "app-release.apk"
                //替换的字符串
                def replaceFile = "wms_${defaultConfig.versionCode}_${defaultConfig.versionName}-${releaseTime()}.apk"
                outputFileName = output.outputFile.name.replace(sourceFile, replaceFile);
                //遗留问题：如何获取当前module的name，如CodeBooke这个名字怎么获取到
            } else {
                //获取签名的名字 variant.signingConfig.name
                //要被替换的源字符串
                def sourceFile = "app-debug.apk"
                //替换的字符串
                def replaceFile = "wms_${defaultConfig.versionCode}_${defaultConfig.versionName}-${variant.buildType.name}.apk"
                outputFileName = output.outputFile.name.replace(sourceFile, replaceFile);
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

greendao {
    schemaVersion 1
    daoPackage 'com.frxs.WMS.greendao.gen'
    targetGenDir 'src/main/java'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'me.dm7.barcodescanner:zxing:1.9.3'
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'com.kyleduo.switchbutton:library:1.4.6'
    compile 'com.github.linger1216:labelview:v1.1.2'
    compile 'com.ms-square:expandableTextView:0.1.4'
    compile 'com.youth.banner:banner:1.4.10'
    compile 'de.hdodenhof:circleimageview:2.2.0'
    compile 'com.joanzapata.android:base-adapter-helper:1.1.11'
    implementation project(':core')
    implementation files('libs/a8-7s.jar')
    implementation files('libs/DataCollection.jar')
    implementation files('libs/gprintersdkv22.jar')
    implementation files('libs/jcc-bate-0.7.3.jar')
    implementation files('libs/ksoap2-android-assembly-2.5.2-jar-with-dependencies.jar')
    compile 'com.allenliu.versionchecklib:library:2.0.5'
}
